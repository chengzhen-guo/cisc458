%  PT Parser Version 5.0
%
% Date:  30 Jun 1980
%        (Revised v5.0 - 10 Dec 2020)
%
% Author:  Alan Rosselet
%
% Abstract
%     This module parses the syntax token stream emitted by the scanner
%     and emits a postfix stream of tokens to the semantic analyzer
%     module.  The parser detects and repairs syntax errors so that the
%     token stream emitted to the semantic analyzer is syntactically
%     legal (except for the special case of declaration phase errors - 
%     see the comment in the Block routine below).

Input :
        % Keywords must be first both here and in stdIdentifiers,
        % and they must appear in the exact same order

        %==============================================
        % Removed div, mod, until, do, array, program,
        %         const, type, procedure, begin, case.      
        pOr                     'or'
        firstKeywordToken = pOr
        pAnd                    'and'
        pNot                    'not'
        pThen                   'then'
        pElse                   'else'
        pOf                     'of'
        pEnd                    'end'
        pFile                   'file'
        pVar                    'var'
        pIf                     'if'
        pWhile                  'while'
        pRepeat                 'repeat'
        % The followings are new like keywords.
        pChoose                 'choose'
        pElseif                 'elseif'
        pFun                    'fun'
        pIs                     'is'
        pLike                   'like'
        pPkg                    'pkg'
        pPublic                 'public'
        pUsing                  'using'
        pVal                    'val'
        pWhen                   'when'
        lastKeywordToken = pWhen 
        %=============================================

        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral
        lastCompoundInputToken = pStringLiteral

        %=============================================
        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus                   '+'
        pMinus                  '-'
        pStar                   '*'
        % Removed pColonEquals.
        pDot                    '.'
        pComma                  ','
        pSemicolon              ';'
        pColon                  ':'
        pEquals                 '='
        pNotEqual               '!='
        pLess                   '<'
        pLessEqual              '<='
        pGreaterEqual           '>='
        pGreater                '>'
        pLeftParen              '('
        pRightParen             ')'
        pLeftBracket            '['
        pRightBracket           ']'
        pDotDot                 '..'
        % The followings are new like non-compound tokens.
        pSlash                  '/'
        pPercent                '%'
        pHash                   '#'
        pBang                   '!'
        pOrBar                  '|'
        pDoubleOrBar            '||'
        pPlusEquals             '+='
        pMinusEquals            '-='
        pStarEquals             '*='
        pSlashEquals            '/='
        pPercentEquals          '%='
        pDoubleEquals           '=='
        lastSyntaxToken = pDoubleEquals;
        %=============================================

Output :
        sIdentifier 
        firstSemanticToken = sIdentifier
        firstCompoundSemanticToken = sIdentifier
        sInteger
        sStringLiteral
        lastCompoundSemanticToken = sStringLiteral
        %=============================================
        sProgram
        sParmBegin
        sParmEnd
        sConst
        % Replaced sType with sLike
        sLike
        sVar
        sProcedure
        sBegin
        sEnd
        sNegate
        sArray
        sFile
        sRange
        sCaseStmt
        sCaseEnd
        sLabelEnd
        sExpnEnd
        sNullStmt
        sAssignmentStmt
        sSubscript
        sCallStmt
        sFieldWidth
        sIfStmt
        sThen
        sElse
        sWhileStmt
        sRepeatStmt
        sRepeatEnd
        sEq
        sNE
        sLT
        sLE
        sGT
        sGE
        sAdd
        sSubtract
        sMultiply
        sDivide
        sModulus
        sInfixOr
        sOr
        sInfixAnd
        sAnd
        sNot
        sNewLine
        sEndOfFile
        % The followings are new Like parser output tokens.
        sPackage
        sPublic
        sConcatenate
        sRepeatString
        sSubstring
        sLength
        sInitialValue
        sCaseElse
        lastSemanticToken = sCaseElse;
        %=============================================

rules

Program :
        'using'  .sProgram
        {
            pIdentifier  .sIdentifier
            [
                | ',':
                | *:
                    >
            ]
        }
        .sParmEnd
        ';'  @Block;

Block :
        % Strictly speaking, standard Like requires that declarations
        % appear only in the order consts, types, vars, procedures.
        % We allow arbitrary intermixing of these instead to preserve
        % programmer sanity. 
        .sBegin
        {[
            | 'pkg':
                .sPackage
                pIdentifier .sIdentifier
                'is'
                @Statement
                'end' ';'
            | 'public':
                [
                    | 'val':
                        .sConst
                        @PublicConstantDefinitions
                    | 'var':
                        .sVar
                        @PublicVariableDeclarations
                    | 'fun':
                        .sProcedure
                        pIdentifier .sIdentifier .sPublic
                        @ProcedureHeading
                        'is' 
                        @Statement
                        'end' ';'
                ]
            | 'val':
                .sConst
                @ConstantDefinitions
            | 'var':
                .sVar
                @VariableDeclarations
            | 'fun':
                .sProcedure
                % procedure name
                pIdentifier .sIdentifier
                @ProcedureHeading
                @Statement
            | pIdentifier:
                @AssignmentOrCallStmt
            | 'if':
                @IfStmt
            | 'choose':
                @CaseStmt
            | 'repeat':
                [
                    | 'while':
                        @WhileStmt
                    | *:
                        @RepeatStmt
                ]
            | ';':
                .sNullStmt
            | *:
                >
        ]}
        .sEnd;

Statement :
        % Moved all functionality to Block.
        @Block;

ConstantDefinitions :
        % Accept one or more named constant definitions.
        pIdentifier .sIdentifier
        '=' @ConstantValue 
        {[
            | ',':
                pIdentifier
                .sIdentifier
                '=' @ConstantValue
            | *:
                >
        ]} ';';

PublicConstantDefinitions :
        % Accept one or more named constant definitions marked as public.
        pIdentifier .sIdentifier .sPublic
        '=' @ConstantValue 
        {[
            | ',':
                pIdentifier
                .sIdentifier
                .sPublic
                '=' @ConstantValue
            | *:
                >
        ]} ';';

VariableDeclarations :
        % Accept only one variable declarations can assign value or use like to assign type.
        pIdentifier .sIdentifier
        @ValueOrLike  ';';

PublicVariableDeclarations :
        % Accept only one variable declarations can assign value or use like to assign type marked as public.
        pIdentifier .sIdentifier .sPublic
        @ValueOrLike  ';';

ConstantValue :
        [
            | pInteger:
                .sInteger
            | pIdentifier:
                .sIdentifier
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | pStringLiteral:
                .sStringLiteral
        ];

UnsignedIntegerConstant :
        [
            | pIdentifier:
                .sIdentifier
            | pInteger:
                .sInteger
        ];

OptionallySignedIntegerConstant :
        [
            | '-':
                @UnsignedIntegerConstant
                .sNegate
            | '+':
                @UnsignedIntegerConstant
            | *:
                @UnsignedIntegerConstant
        ];

ValueOrLike :
        % Allow variables to assign value or declare type with like statement.
        [
            | '=':
                .sInitialValue
                @Expression
                .sExpnEnd
            | *:
                @LikeClause
        ];

LikeClause :
        [
            | '[':
                .sArray @ConstantValue
                ']'
            | *:
        ]
        ':' 
        [
            | 'file':
                .sFile
            | *:
        ]
        'like'
        .sLike 
        [
            | pIdentifier:
                .sIdentifier
            | *:
                @ConstantValue
        ];

ProcedureHeading :
        % Accept zero or more procedure formal parameter declarations.
        [
            | '(':
                {
                    % formal parameter identifier
                    [
                        | 'var':
                            pIdentifier  .sIdentifier
                            .sVar
                        | *:
                            pIdentifier  .sIdentifier
                    ]
                    @LikeClause                    
                    [
                        | ',':                            
                        | *:
                            >
                    ]
                }
                ')'
            | *:
        ]
        .sParmEnd;

AssignmentOrCallStmt :
        % Added like assignment operations.
        [
            | '=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                @Expression
                .sExpnEnd
            | '+=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sAdd
                .sExpnEnd
            | '-=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sSubtract
                .sExpnEnd
            | '*=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sMultiply
                .sExpnEnd
            | '/=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sDivide
                .sExpnEnd
            | '%=':
                .sAssignmentStmt
                .sIdentifier    % identifier accepted in the Statement rule
                .sIdentifier
                @Expression
                .sModulus
                .sExpnEnd
            | '[':
                .sAssignmentStmt
                .sIdentifier
                .sSubscript
                @Expression
                .sExpnEnd
                ']'  '='
                @Expression
                .sExpnEnd
            | *:
                @CallStmt
        ] ';';

CallStmt :
        .sCallStmt
        .sIdentifier    % procedure identifier accepted in the Statement rule
        % accept zero or more actual parameters
        [
            | '(':
                {
                    @Expression
                    .sExpnEnd
                    % allow a field width specification to occur in calls
                    % to the standard procedures write and writeln
                    [
                        | ':':
                            .sFieldWidth
                            @Expression
                            .sExpnEnd
                        | *:
                    ]
                    [
                        | ',':
                        | *:
                            >
                    ]
                }
                ')'
            | *:        % no actual parameters
        ]
        .sParmEnd;

IfStmt :
        .sIfStmt
        @Expression
        .sExpnEnd
        'then'  .sThen
        @Statement
        [
            | 'elseif':
                .sElse 
                .sBegin
                @IfStmt
                .sEnd
            | *:
			[
				| 'else':
                    .sElse
                    @Statement
				| *:
			]
            'end' ';'
        ];

CaseStmt :
        .sCaseStmt
        @Expression
        .sExpnEnd
        'of'
        'when'
        @CaseAlternative 
        {[
            | 'when':
                @CaseAlternative 
            | *:
                >
        ]}
        .sCaseEnd
        [
            | 'else':
                .sCaseElse
                @Statement
            | *:
        ]
        'end' ';';

CaseAlternative :
        % A case alternative is a statement labelled by
        % one or more optionally signed integer constants        
        {
            @OptionallySignedIntegerConstant
            [
                | ',':
                | *:
                    >
            ]
        }
        .sLabelEnd
        'then'  @Statement;

WhileStmt :
        % repeat while statement.
        .sWhileStmt
        @Expression
        .sExpnEnd
        @Statement
        'end' ';';

RepeatStmt :
        .sRepeatStmt
        {
            @Statement
            [
                | 'while':
                    .sRepeatEnd
                    >
            ]
        }
        @Expression 
	    ';'
        .sNot
        .sExpnEnd;

Expression :
        % Changed to Like operators.
        @SimpleExpression
        [
            | '==':
                @SimpleExpression  .sEq
            | '!=':
                @SimpleExpression  .sNE
            | '<':
                @SimpleExpression  .sLT
            | '<=':
                @SimpleExpression  .sLE
            | '>':
                @SimpleExpression  .sGT
            | '>=':
                @SimpleExpression  .sGE
            | *:
        ];

SimpleExpression :
        [
            | '+':
                @Term
            | '-':
                @Term  .sNegate
            | *:
                @Term
        ]
        {[
            | '+':
                @Term  .sAdd
            | '-':
                @Term  .sSubtract
            | 'or':
                .sInfixOr  @Term  .sOr
            | '|':
                @Term  .sConcatenate
            | '||':
                @Term  .sRepeatString
            | *:
                >
        ]};

Term :
        % Changed to Like operators divide and modulus.
        @Factor
        {[
            | '*':
                @Factor  .sMultiply
            | '/':
                @Factor  
                [
                    | ':':
                        @Factor .sSubstring
                    | *:
                        .sDivide
                ]
            | '%':
                @Factor  .sModulus
            | 'and':
                .sInfixAnd  @Factor  .sAnd
            | *:
                >
        ]};

Factor :
        % Added string operation rule calling and string length handling.
        [
            | pIdentifier:
                .sIdentifier
                @IdentifierExtension
            | pInteger:
                .sInteger
            | '(':
                @Expression  ')'
            | 'not':
                @Factor
                .sNot
            | pStringLiteral:
                .sStringLiteral
            | '#':
                pStringLiteral .sStringLiteral
                .sLength
            | 'file':
                .sFile '(' @Expression ')'
                .sExpnEnd
        ];

IdentifierExtension :
        % Handle subscripts and function actual parameter lists
        [
            | '[':
                .sSubscript
                @Expression  ']'
                .sExpnEnd
            | '(':
                .sParmBegin
                @Expression  ')'
                .sExpnEnd
                .sParmEnd
            | *:
        ];

end